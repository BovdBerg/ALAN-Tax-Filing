users
	anonymous

interfaces

root {
	'Management': group {
		'Discount periods': collection ['Period'] {
			'Period': text
			'Percentage' : number 'percent'
			'Minimal spendings': number 'eurocent'
		}

		'VAT percentage': number 'percent'

		'Beverages types': collection ['Beverage type'] {
			'Beverage type': text
		}

		'Tables': collection ['Table number'] {
			'Table number': text
			'Seatings': number 'chairs'
		}
	}

	'Menu': collection ['Item name'] {
		'Item name': text
		'Selling price': number 'eurocent'
		'Item type': stategroup (
			'Dish' {
				'Dish type': stategroup (
					'Appetizer' { }
					'Main course' { }
					'Dessert' { }
				)
			}
			'Beverage' {
				'Beverage type': text -> ^ ^ .'Management' .'Beverages types'[]
			}
		)
	}

	'Orders': collection ['Order'] {
		'Order': text
		'Order type': stategroup (
			'Takeaway' { }
			'In-house' {
				'Table': text -> ^ ^ .'Management' .'Tables'[]
			}
		)
		'Order lines': collection ['Order line'] {
			'Order line': text
			'Item': text -> ^ ^ .'Menu'[]
			'Amount': number 'units'
			'Line total': number 'eurocent' = product (
				.'Amount' as 'units',
				>'Item'.'Selling price'
			)
		}
		'Subtotal': number 'eurocent' = sum .'Order lines'* .'Line total'
	}
}

numerical-types
	'eurocent'
	= 'units' * 'eurocent'
		@numerical-type: (
			label: "Euro"
			decimals: 2
		)
	'chairs'
	'units'
	'percent'
